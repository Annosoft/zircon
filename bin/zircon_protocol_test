#!/usr/bin/env perl

use strict;
use warnings;
use feature qw( switch );

use Getopt::Long;
use Try::Tiny;
use Tk;
use Zircon::Protocol;
use Zircon::TkZMQ::Context;

# parse the command line
my $remote_endpoint;
GetOptions(
    'remote_endpoint=s' => \$remote_endpoint,
    ) or die 'command line error';
my $is_child =
    defined $remote_endpoint
    && $remote_endpoint ne '';

my $app_id =
    sprintf 'Zircon Protocol (%s)'
    , $is_child ? 'child' : 'parent';

# create the main window
my $main_window = Tk::MainWindow->new;
$main_window->title($app_id);

# create the Zircon connection
my $endpoint =
    sprintf 'zircon_protocol_test_%s_%06d'
    , $main_window->id, int(rand(1_000_000));
my $server = Server->new;
my $context = Zircon::TkZMQ::Context->new(
    '-widget' => $main_window);
my $protocol = Zircon::Protocol->new(
    '-app_id'       => $app_id,
    '-context'      => $context,
    '-endpoint'     => $endpoint,
    '-server'       => $server,
    );

# create the GUI

my $zircon_message = '';

my $button_frame =
    $main_window->Frame->pack('-fill' => 'both');

sub wrap {
    my ($callback) = @_;
    $zircon_message = '';
    try { $callback->(); }
    catch {
        chomp $_;
        $zircon_message = "Error: $_";
    };
    return;
}

sub ping {
    wrap(
        sub {
            $protocol->send_ping(
                sub {
                    my ($result) = @_;
                    $zircon_message =
                        sprintf 'Client: ping: %s', _message($result);
                });
        });
    return;
}

sub shutdown {
    wrap(
        sub {
            $protocol->send_shutdown_clean(
                sub {
                    my ($result) = @_;
                    $zircon_message =
                        sprintf 'Client: shutdown: %s', _message($result);
                });
        });
    return;
}

sub goodbye {
    wrap(
        sub {
            $protocol->send_goodbye_exit(
                sub { CORE::exit; }); # because Perl/Tk overrides exit()
        });
    return;
}

Tk::grid(
    $button_frame->Button(
        '-text'    => 'Ping',
        '-command' => \&ping),
    $button_frame->Button(
        '-text'    => 'Goodbye',
        '-command' => \&goodbye),
    $button_frame->Button(
        '-text'    => 'Shutdown',
        '-command' => \&shutdown),
    $button_frame->Button(
        '-text'    => 'Exit',
        '-command' => \&exit),
    '-sticky' => 'nsew');

$button_frame->gridColumnconfigure($_, '-weight' => 1) for 0..1;

$main_window
    ->Label(
    '-width'        => 50,
    '-textvariable' => \$zircon_message,
    '-relief'       => 'sunken')
    ->pack('-fill' => 'both');

# parent forks child, child sends handshake back to parent
if ($is_child) {
    $zircon_message = '';
    $protocol->send_handshake(
        $remote_endpoint,
        sub {
            my ($result) = @_;
            $zircon_message =
                sprintf 'Client: handshake: %s'
                , _message($result);
        });
}
else {
    my $pid = fork;
    defined $pid
        or die 'fork() failed';
    unless ($pid) {
        # child, so exec()
        exec $0, @ARGV, '-remote_endpoint' => $endpoint;
        die 'exec() failed';
    }
}

MainLoop();

exit;

sub _message {
    my ($result) = @_;
    for ($result) {
        when ($_->isa('Zircon::Protocol::Result::Reply')) {
            return $result->success ? 'succeeded' : 'failed';
        }
        when ($_->isa('Zircon::Protocol::Result::Timeout')) {
            return 'timeout';
        }
        when (ref) {
            return sprintf 'unknown result: class: %s', ref;
        }
    }
    return 'unknown result';
}

package Server;

use base qw( Zircon::Protocol::Server );

sub new {
    my ($pkg) = @_;
    my $new = { };
    bless $new, $pkg;
    return $new;
}

sub zircon_server_log {
    my ($self, $message) = @_;
    $zircon_message =
        sprintf 'Server: %s', $message;
    warn "$zircon_message\n";
    return;
}

=head1 AUTHOR

Ana Code B<email> anacode@sanger.ac.uk
